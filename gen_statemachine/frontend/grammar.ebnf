# Production rules
# Used as a design aid only - not strictly valid EBNF :)

root = KEYWORD_START_UML [LABEL] NEWLINE declarations KEYWORD_END_UML

declarations = {declaration NEWLINE}+

declaration = state_declaration | state_alias_declaration | transition_declaration | anchored_note_declaration | floating_note_declaration | comment | state_label

state_declaration = KEYWORD_STATE NAME [STEREOTYPE] [COLON BEHAVIOR] NEWLINE |
                    KEYWORD_STATE NAME [STEREOTYPE] OPEN_CURLY_BRACKET NEWLINE declarations CLOSE_CURLY_BRACKET NEWLINE

state_alias_declaration = KEYWORD_STATE QUOTATION LABEL QUOTATION KEYWORD_AS NAME [STEREOTYPE] [COLON BEHAVIOR] NEWLINE

state_label = NAME COLON BEHAVIOR

transition_declaration = (NAME | INITIAL_FINAL_STATE) ARROW (NAME | INITIAL_FINAL_STATE) [STEREOTYPE] NEWLINE |
                         (NAME | INITIAL_FINAL_STATE) ARROW (NAME | INITIAL_FINAL_STATE) [STEREOTYPE] COLON transition_label NEWLINE

anchored_note_declaration = KEYWORD_NOTE (KEYWORD_LEFT_OF | KEYWORD_RIGHT_OF) NAME COLON LABEL NEWLINE |
                            KEYWORD_NOTE (KEYWORD_LEFT_OF | KEYWORD_RIGHT_OF) NAME NEWLINE {LABEL NEWLINE}+ KEYWORD_END_UML KEYWORD_NOTE NEWLINE

floating_note_declaration = KEYWORD_NOTE QUOTATION LABEL QUOTATION KEYWORD_AS NAME NEWLINE

comment = APOSTROPHE LABEL |
          START_BLOCK_COMMENT NEWLINE {LABEL NEWLINE}+ END_BLOCK_COMMENT

transition_label = [TRIGGER] [OPEN_SQ_BRACKET GUARD CLOSE_SQ_BRACKET] [FORWARD_SLASH BEHAVIOR]